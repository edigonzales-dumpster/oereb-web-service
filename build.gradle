buildscript {
	ext {
		springBootVersion = '2.0.0.M6'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("se.transmode.gradle:gradle-docker:1.2")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'docker'

group = 'ch.so.agi.oereb'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa') 
	//compile('org.springframework.boot:spring-boot-starter-data-rest')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('com.bedatadriven:jackson-datatype-jts:2.4')	
	compile('org.postgresql:postgresql:42.1.4')
	compile('org.hibernate:hibernate-spatial:5.2.12.Final')
	runtime('org.springframework.boot:spring-boot-devtools')
	testCompile('org.springframework.boot:spring-boot-starter-test')		
}

bootJar {
    launchScript()
}

sourceSets {
	generated {
		java {
			srcDir 'src/generated/java'
		}
		resources {
			srcDir 'src/generated/resources'
		}
	}
    main {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }	
	test {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
}

task buildDocker(type: Docker) {
	tag = "sogis/${applicationName}"
	tagVersion = 'latest'
	baseImage = 'frolvlad/alpine-oraclejdk8:slim'
	push = project.hasProperty('push') // <-> "./gradlew buildDocker -Ppush"
	applicationName = jar.baseName
	addFile {
		from jar
		rename {'app.jar'}
	}
  	entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar'])
}
